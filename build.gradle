apply plugin: 'java'

defaultTasks 'clean', 'test'

sourceSets {
  main {
    java {srcDir 'src'}
    resources {srcDir 'src'}
  }
  test {
    java {srcDir 'test'}
    resources {srcDir 'test'}
  }
}

repositories {
  mavenCentral()
}

dependencies {
  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:hamcrest-all:1.3'
  testCompile 'com.codeborne:selenide:4.10.01'
  testCompile 'org.seleniumhq.selenium:selenium-safari-driver:3.8.1'
}

task libs(type: Sync) {
  from configurations.testCompile
  into "$buildDir/lib"
}

classes.dependsOn libs

[compileJava, compileTestJava]*.options.collect {options -> options.encoding = 'UTF-8'}
[compileJava, compileTestJava]*.options.collect {options -> options.debug = true}
[compileJava, compileTestJava]*.sourceCompatibility = 1.8
[compileJava, compileTestJava]*.targetCompatibility = 1.8

tasks.withType(Test).all { testTask ->
  testTask.systemProperties = [
    'file.encoding': 'UTF-8',
    'BUILD_URL': System.getenv()['BUILD_URL']
  ]
  testTask.testLogging.showStandardStreams = true
  testTask.exclude 'org/selenide/examples/google/classic_page_object/**/*'
  testTask.outputs.upToDateWhen { false }
}

task chrome(type: Test) {
  systemProperties['selenide.browser'] = 'chrome'
}

task firefox(type: Test) {
  systemProperties['selenide.browser'] = 'firefox'
}

task ie(type: Test) {
  systemProperties['selenide.browser'] = 'ie'
}

task edge(type: Test) {
  systemProperties['selenide.browser'] = 'edge'
}

task htmlunit(type: Test) {
  systemProperties['selenide.browser'] = 'htmlunit'
}

task phantomjs(type: Test) {
  systemProperties['selenide.browser'] = 'phantomjs'
}

// You must enable the 'Allow Remote Automation' option in Safari's Develop menu to control Safari via WebDriver.
task safari(type: Test) {
  systemProperties['selenide.browser'] = 'safari'
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.4.1'
  jarFile = './gradle/wrapper/gradle-wrapper.jar'
  scriptFile = './gradle'
}