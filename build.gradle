plugins {
  id "java"
}

defaultTasks 'clean', 'test'

sourceSets {
  main {
    java {srcDir 'src'}
    resources {srcDir 'src'}
  }
  test {
    java {srcDir 'test'}
    resources {srcDir 'test'}
  }
}

repositories {
  mavenCentral()
}

dependencies {
  testImplementation 'org.junit.jupiter:junit-jupiter:5.11.0'
  testImplementation 'com.codeborne:selenide:7.4.3'
  testImplementation('org.seleniumhq.selenium:selenium-safari-driver:4.24.0') { transitive = false }
  testImplementation 'org.assertj:assertj-core:3.26.3'
  testRuntimeOnly 'org.slf4j:slf4j-simple:2.0.16'
}

[compileJava, compileTestJava]*.options.collect {options -> options.encoding = 'UTF-8'}
[compileJava, compileTestJava]*.options.collect {options -> options.debug = true}

tasks.withType(Test).configureEach { testTask ->
  testTask.systemProperties = [
    'file.encoding': 'UTF-8',
    'BUILD_URL': System.getenv()['BUILD_URL']
  ]
  testTask.testLogging.showStandardStreams = true
  testTask.exclude 'org/selenide/examples/google/classic_page_object/**/*'
  testTask.outputs.upToDateWhen { false }
  testTask.useJUnitPlatform()
}

task chrome(type: Test) {
  systemProperties['selenide.browser'] = 'chrome'
  systemProperties['selenide.headless'] = 'true'
}

task firefox(type: Test) {
  systemProperties['selenide.browser'] = 'firefox'
  systemProperties['selenide.headless'] = 'true'
}

task ie(type: Test) {
  systemProperties['selenide.browser'] = 'ie'
}

task edge(type: Test) {
  systemProperties['selenide.browser'] = 'edge'
}

// You must enable the 'Allow Remote Automation' option in Safari's Develop menu to control Safari via WebDriver.
task safari(type: Test) {
  systemProperties['selenide.browser'] = 'safari'
}
